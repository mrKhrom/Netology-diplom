
#Область ПрограммныйИнтерфейс

Функция ОбработатьВходящийЗапрос(Запрос) Экспорт
	
	ДанныеЗапроса = ДанныеЗапроса(Запрос);
	//ДанныеЗапроса = Запрос;
	Если ДанныеЗапроса.Свойство("message") Тогда
		Возврат ОбработатьСообщение(ДанныеЗапроса.message);
	КонецЕсли;
	
	Возврат вкм_РаботаСHTTP.ПростойУспешныйОтвет(); 
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеЗапроса(Запрос)
	
	ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	Возврат вкм_РаботаСHTTP.ОбъектJSON(ТелоЗапроса);
	
КонецФункции

Функция ОбработатьСообщение(ДанныеСообщения)
	
	УстановитьПривилегированныйРежим(Истина);
	Если ДанныеСообщения.Свойство("from") Тогда
		ЗафиксироватьОтправителя(ДанныеСообщения.from);
	КонецЕсли;  
	
	ИдентификаторЧата = Формат(ДанныеСообщения.chat.id,"ЧГ=0");
	
	Если ДанныеСообщения.Свойство("ЭтоОтправка") Тогда
		ДанныеСообщения.Удалить("ЭтоОтправка"); 
	Иначе
		Константы.ВКМ_ИдентификаторГруппыДляОповещения.Установить(ИдентификаторЧата); 
	КонецЕсли;
	
	Если ДанныеСообщения.Свойство("text") Тогда 
		ТекстСообщения = ДанныеСообщения.text;
	Иначе
		ТекстСообщения = "Пустое сообщение";	
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("method", "sendMessage");
	ДанныеОтвета.Вставить("chat_id", ИдентификаторЧата);
	ДанныеОтвета.Вставить("text", ТекстСообщения);
	
	Возврат вкм_РаботаСHTTP.ОтветJSON(ДанныеОтвета);
	
КонецФункции

Функция ЗафиксироватьОтправителя(ДанныеОтправителя)
	
	УстановитьПривилегированныйРежим(Истина);
	УчетнаяЗапись = Справочники.ВКМ_УчетныеЗаписиТелеграмм.НайтиПоКоду(Формат(ДанныеОтправителя.id,"ЧГ=0"));
	Если НЕ ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		УчетнаяЗапись = Справочники.ВКМ_УчетныеЗаписиТелеграмм.СоздатьЭлемент();
	Иначе
		Возврат УчетнаяЗапись;
	КонецЕсли;
	
	ЧастиИмя = Новый Массив;
	Если ДанныеОтправителя.Свойство("first_name") Тогда
		ЧастиИмя.Добавить(ДанныеОтправителя.first_name);
	КонецЕсли;
	Если ДанныеОтправителя.Свойство("last_name") Тогда
		ЧастиИмя.Добавить(ДанныеОтправителя.last_name);
	КонецЕсли; 
	Если ДанныеОтправителя.Свойство("username") Тогда
		ЧастиИмя.Добавить(ДанныеОтправителя.username);
	КонецЕсли;
	
	УчетнаяЗапись.Код = Формат(ДанныеОтправителя.id,"ЧГ=0");
	УчетнаяЗапись.Наименование = СтрСоединить(ЧастиИмя);
	УчетнаяЗапись.Имя = СтрСоединить(ЧастиИмя);
	УчетнаяЗапись.Записать();
	
	Возврат УчетнаяЗапись.Ссылка;	
КонецФункции

Процедура РассылкаСообщений() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторЧата = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	Если НЕ ЗначениеЗаполнено(ИдентификаторЧата) Тогда  
		вкм_РаботаСHTTP.ОтветОбОшибке("Не установлена константа идентификатора чата");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ    
	|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Уведомление,
	|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения КАК ТекстСообщения
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту
	|ГДЕ
	|	НЕ ВКМ_УведомленияТелеграмБоту.Отправлено";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать(); 
	
	TokenTelegramm=ПолучитьTokenTelegramm(); 
	Соединение = Новый HTTPСоединение("api.telegram.org",443,,,,,Новый ЗащищенноеСоединениеOpenSSL());        
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("content-type", вкм_РаботаСHTTP.ТипКонтентаJSON());
	ЗапросHTTP = Новый HTTPЗапрос("bot"+TokenTelegramm+"/sendMessage", Заголовки);
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДанныеСообщения = Новый Структура;      
		ДанныеСообщения.Вставить("text",  ВыборкаДетальныеЗаписи.ТекстСообщения); 
		ДанныеСообщения.Вставить("chat_id", ИдентификаторЧата);  
		ОбъектJSON = вкм_РаботаСHTTP.СтрокаJSON(ДанныеСообщения);
		ЗапросHTTP.УстановитьТелоИзСтроки(ОбъектJSON);
		ОтветJSON = Соединение.ОтправитьДляОбработки(ЗапросHTTP);
		Если ОтветJSON.КодСостояния = 200 ТОгда 
			вкм_РаботаСHTTP.ЗаписьЛога("Send", ЗапросHTTP, ОтветJSON);
			УведомлениеОбъект = ВыборкаДетальныеЗаписи.Уведомление.ПолучитьОбъект(); 
			УведомлениеОбъект.Отправлено = Истина;
			УведомлениеОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	УдалениеОтправленныхСообщений();
КонецПроцедуры  

Процедура УдалениеОтправленныхСообщений() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Уведомление
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту
	|ГДЕ
	|	ВКМ_УведомленияТелеграмБоту.Отправлено";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		УведомлениеОбъект = ВыборкаДетальныеЗаписи.Уведомление.ПолучитьОбъект(); 
		УведомлениеОбъект.Удалить();
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьTokenTelegramm()
	
	Токен = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	Если НЕ ЗначениеЗаполнено(Токен) Тогда
		ЗаписьЖурналаРегистрации("Константы.ВКМ_ТокенУправленияТелеграмБотом", УровеньЖурналаРегистрации.Ошибка,,,"Не установлено значение константы");		
	КонецЕсли;
	Возврат Токен;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	ДлительнаяОперация = СоздатьНаСервере();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьВФонеЗавершение", ЭтотОбъект); 
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация,ОповещениеОЗавершении);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьНаСервере()
	
	ИмяМетода = "Обработки.ВКМ_МассовоеСозданиеАктов.ПолучитьДанныеИзРегистровМенеджер"; 
	//@skip-check form-self-reference
	Возврат ДлительныеОперации.ВыполнитьФункцию(ЭтаФорма.УникальныйИдентификатор, ИмяМетода, Объект.Организация, Объект.Период);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт  
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ВывестиРезультат(Результат.АдресРезультата);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиРезультат(АдресРезультата)
	
	Данные = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Объект.СозданныеРеализации.Очистить();
	
	Если ТипЗнч(Данные) = Тип("Массив") Тогда
		
		Для Каждого элДанные из Данные Цикл
			
			Если Не ЗначениеЗаполнено(элДанные.ДокументСсылка) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = Объект.СозданныеРеализации.Добавить();
			НоваяСтрока.Контрагент = элДанные.Контрагент;
			НоваяСтрока.Договор = элДанные.Договор;
			НоваяСтрока.Реализация = элДанные.ДокументСсылка;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 